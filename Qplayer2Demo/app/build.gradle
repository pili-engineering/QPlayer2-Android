import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

private static String getBuildTime() {
    DateTimeFormatter dtf = DateTimeFormatter.ofPattern("EEEE d MMMM yyyy HH:mm:ss")
    LocalDateTime now = LocalDateTime.now()
    String dateTime = dtf.format(now)
    return "\"${dateTime}\""
}

android {
    compileSdkVersion 32
    buildToolsVersion "32.0.0"

    defaultConfig {
        applicationId "com.qiniu.qplayer2"
        minSdkVersion 19
        targetSdkVersion 32
        versionCode 1
        versionName "$qplayer2_version"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        qplayer2Config {
            keyAlias "key-qplayer2"
            keyPassword "123456"
            storePassword "123456"
            storeFile rootProject.file("key")
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.qplayer2Config
        }
        debug {
            debuggable = true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.qplayer2Config
        }

    }

    applicationVariants.all {
        variant ->
            variant.buildConfigField 'String', 'QPLAYER2_DEMO_BUILD_TIME', getBuildTime()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "android.arch.lifecycle:runtime:1.1.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.preference:preference-ktx:1.1.0'
    implementation "io.reactivex.rxjava3:rxjava:3.1.3"
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation("io.coil-kt:coil:1.2.0")
    implementation 'com.google.code.gson:gson:2.8.9'

    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation ("com.squareup.okhttp3:okhttp:3.12.12"){
        force = true //API 19 support
    }
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    if (dependency_type == "remote") {
        implementation("com.qiniu:qplayer2-core:${qplayer2_version}")
        implementation("com.qiniu:qplayer2-ext:${qplayer2_version}")

    } else if(dependency_type == "source"){

        implementation project(path: ':qplayer2-ext')
        implementation project(path: ':qplayer2-core')
    } else {
        implementation files("libs/qplayer2-core-${qplayer2_version}-release.aar", "libs/qplayer2-ext-${qplayer2_version}-release.aar")
    }


    implementation 'com.tencent.bugly:crashreport:latest.release' //其中latest.release指代最新Bugly SDK版本号，也可以指定明确的版本号，例如2.1.9
    implementation 'com.tencent.bugly:nativecrashreport:latest.release' //其中latest.release指代最新Bugly NDK版本号，也可以指定明确的版本号，例如3.0

}